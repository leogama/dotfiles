# .zshrc: executed by ZSH interactive shells
# vi: set ft=zsh :

# Load utils library and POSIX shell stuff.
source "$XDG_CONFIG_HOME/sh/rc"


### Initiate plugin manager ###

source "$XDG_DATA_HOME/zinit/zinit.git/zinit.zsh"

# compinit may have been called by system's rc.
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit


### Options ###

# History behavior.
SAVEHIST=10000
HISTSIZE=$SAVEHIST
HISTFILE="$ZDOTDIR/history"
setopt hist_ignore_all_dups hist_ignore_space hist_no_store hist_reduce_blanks \
    share_history
histchars[2]='|'  # use '|' for rapid substitutions, ex.:  % |foo|bar:G
bindkey '^R' history-incremental-search-backward

# Globbing behavior.
setopt csh_null_glob ksh_glob

# Split unquoted variables into words.
setopt sh_word_split

# Allow comments in interactive mode.
# NOTE: useful for saving to history without executing.
setopt interactive_comments


### Commands ###

# latexrun fork from aclementes (https://github.com/ahueck/latexrun/tree/fixes)
zinit ice as=command ver=fixes if="is_command latex"; zinit load ahueck/latexrun
zinit ice as=command from=gh-r if="! is_command lf"; zinit load gokcehan/lf
#zinit ice as=command; zinit load raylee/tldr  # Haskell version is better

# Load aliases and autoload functions.
try_source "$ZDOTDIR/alias.zsh"
if [[ -d "$ZDOTDIR/alias.d" ]]; then
    for file in "$ZDOTDIR/alias.d"/*.zsh; do
        try_source "$file"
    done
fi

if [[ -d "$ZDOTDIR/autoload" ]]; then
    fpath=("$ZDOTDIR/autoload" $fpath)
    autoload -U ${fpath[1]}/*(:t)
fi


### Plugins ###

zinit load zsh-users/zsh-autosuggestions
zinit snippet "$ZDOTDIR/custom/namedir.zsh"

# Oh-My-ZSH! theme
setopt promptsubst
zinit snippet OMZ::lib/async_prompt.zsh
zinit snippet OMZ::lib/git.zsh
zinit snippet OMZ::themes/agnoster.zsh-theme


### Completion ###

autoload -U compinit; compinit
zinit cdreplay  # replay compdefs

# Bash completions
if [[ -d "$ZDOTDIR/bashcomp" ]]; then
    autoload -U bashcompinit; bashcompinit
    BASH_VERSION=ZSH
    for bashcomp in "$ZDOTDIR/bashcomp/"*; do
        source "$bashcomp"
    done
    unset BASH_VERSION
fi

# Custom
zstyle ':completion:*:complete:ssh:*:*' tag-order '!users'  # just complete hosts


try_source "$ZDOTDIR/.zshrc.local"
