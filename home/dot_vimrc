if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = expand('$HOME/.config')      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = expand('$HOME/.local/share') | endif
if empty($XDG_STATE_HOME)  | let $XDG_STATE_HOME  = expand('$HOME/.local/state') | endif

set runtimepath^=$XDG_CONFIG_HOME/vim
set runtimepath+=$XDG_DATA_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after

set packpath^=$XDG_DATA_HOME/vim,$XDG_CONFIG_HOME/vim
set packpath+=$XDG_CONFIG_HOME/vim/after,$XDG_DATA_HOME/vim/after

call mkdir($XDG_DATA_HOME.'/vim/spell', 'p')
set backupdir=$XDG_STATE_HOME/vim/backup | call mkdir(&backupdir, 'p')
set directory=$XDG_STATE_HOME/vim/swap   | call mkdir(&directory, 'p')
set undodir=$XDG_STATE_HOME/vim/undo     | call mkdir(&undodir,   'p')
set viewdir=$XDG_STATE_HOME/vim/view     | call mkdir(&viewdir,   'p')

if !has('nvim')
    set viminfofile=$XDG_STATE_HOME/vim/viminfo
endif

let g:netrw_home = expand('$XDG_DATA_HOME/vim')
let g:plug_home = expand('$XDG_DATA_HOME/vim/plugged')

let $MYVIMRC = expand('$XDG_CONFIG_HOME/vim/vimrc')
if empty($NOVIMRC) && filereadable($MYVIMRC)
    source $MYVIMRC
endif
